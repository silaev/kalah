buildscript {
    ext {
        springBootVersion = '2.1.6.RELEASE'
        mockitoVersion = '3.0.0'
        junitVersion = '5.3.1'
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "io.franzbecker.gradle-lombok" version "3.1.0"
    id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.silaev.kalah'
version = '1.0.0'
sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenCentral()
}

lombok {
    version = "1.18.8"
}

springBoot {
    mainClassName = 'com.silaev.wms.WmsApplication'
}

ext['mockito.version'] = "${mockitoVersion}"

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-web')

    runtimeOnly('org.springframework.boot:spring-boot-devtools')

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'

        testCompile("org.junit.jupiter:junit-jupiter-params:${junitVersion}")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    }

    testCompile("org.mockito:mockito-junit-jupiter:3.0.0")
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
}

tasks.withType(Test) { Test task ->
    task.useJUnitPlatform { JUnitPlatformOptions options ->
        options.excludeTags 'integration-test'
    }

    task.failFast = true

    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'

    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task integrationTest(type: Test) { Test task ->
    task.useJUnitPlatform { JUnitPlatformOptions options ->
        options.includeTags 'integration-test'
    }

    task.failFast = true

    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
    finalizedBy jacocoTestReport
}

wrapper {
    gradleVersion = '5.6.2'
    distributionType = Wrapper.DistributionType.ALL
}

jacoco {
    toolVersion = "0.8.4"
}

private static List<String> getSonarExclusions() {
    new File("testcov.exclude").readLines()
}

jacocoTestReport {
    executionData.from = fileTree(buildDir).include("/jacoco/*.exec")
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: getSonarExclusions())
        })
    }
}

jacocoTestCoverageVerification {
    executionData.from = fileTree(buildDir).include("/jacoco/*.exec")
    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: getSonarExclusions())
        })
    }
}

check.dependsOn jacocoTestCoverageVerification